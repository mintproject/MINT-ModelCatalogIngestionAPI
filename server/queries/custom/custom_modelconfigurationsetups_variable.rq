#+ summary: Given a rdf type, returns all the resources related to the type

PREFIX sd: <https://w3id.org/okn/o/sd#>
PREFIX sdm: <https://w3id.org/okn/o/sdm#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>


CONSTRUCT {
    ?setup ?predicate ?prop .
    ?prop a ?type .
    ?output_resource ?output_resource_p ?output_resource_o .
    ?variable_presentation_p_output ?variable_o_output ?variable_p_output
}
WHERE {
        GRAPH ?_g_iri {
            {
                SELECT DISTINCT ?setup {
                  ?setup a sdm:ModelConfigurationSetup .
                  {
                    ?setup sd:hasOutput ?output.
                    ?output sd:hasPresentation ?vp.
                    ?vp sd:hasStandardVariable ?sv.
                  } UNION {
                    ?model sd:hasVersion ?version.
                    ?version sd:hasConfiguration ?configuration.
                    ?configuration sd:hasSetup ?setup .
                    ?model sdm:usefulForCalculatingIndex ?sv.
                  }
                  ?sv rdfs:label ?variableLabel .
                  FILTER REGEX(?variableLabel, ?_label, "i")
                }
            }
            {
                ?setup ?predicate ?prop
                OPTIONAL {
                 ?prop a ?type
                }
            }
            UNION {
                ?setup  sd:hasOutput ?output_resource .
                ?output_resource ?output_resource_p ?output_resource_o
                OPTIONAL {
                   ?output_resource sd:hasPresentation ?variable_presentation_output .
                   ?variable_presentation_output ?variable_presentation_o_output ?variable_presentation_p_output .
                   ?variable_presentation_p_output ?variable_o_output ?variable_p_output

                }
            }

      }
}
